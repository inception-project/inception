// Licensed to the Technische Universität Darmstadt under one
// or more contributor license agreements.  See the NOTICE file
// distributed with this work for additional information
// regarding copyright ownership.  The Technische Universität Darmstadt 
// licenses this file to you under the Apache License, Version 2.0 (the
// "License"); you may not use this file except in compliance
// with the License.
//  
// http://www.apache.org/licenses/LICENSE-2.0
// 
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

[[sect_constraints]]
= Constraints

Constraints reorder the choice of tags based on the context of an annotation.
For instance, for a given lemma, not all possible part-of-speech tags are sensible.
Constraint rules can be set up to reorder the choice of part-of-speech tags such that the relevant tags are listed first.
This speeds up the annotation process as the annotator can choose from the relevant tags more conveniently.

The choice of tags is not limited, only the order in which they are presented to the annotator. 
Thus, if the project manager has forgotten to set up a constraint or did possible not consider an oddball case, the annotator can still make a decision. 


== Importing constraints

To import a constraints file, go to *Project* and click on the particular project name.
On the left side of the screen, a tab bar opens.
Choose *Constraints*. You can now choose a constraint file by clicking on *Choose Files*.
Then, click on *Import*.
Upon import, the application checks if the constraints file is well formed.
If they conform to the rules of writing constraints, the constraints are applied. 

== Implementing constraint sets

A *constraint set* consists of two components:

* import statement
* scopes
* Import statements* are composed in the following way:

[source,text]
----
import <fully_qualified_name_of_layer> as <shortName>;
----

It is necessary to declare short names for all fully qualified names because only short names can be used when writing a constraint rule. Short names cannot contain any dots or special characters, only letters, numbers, and the underscore.

NOTE: All identifiers used in constraint statements are *case sensitive*.

NOTE: If you are not sure what the fully qualified name of a layer is, you can look it up going 
      to *Layers* in *Project settings*. Click on a particular layer and you can view the fully qualified 
      name under *Technical Properties*.

*Scopes* consist of a *scope name* and one or more *rules* that refer to a particular annotation layer and define restrictions for particular conditions. For example, it is possible to reorder the applicable tags for a POS layer, based on what kind of word the annotator is focusing on. 

While scope names can be freely chosen, scope rules have a fixed structure. They consist of *conditions* and *restrictions*, separated by an arrow symbol (`->`).
Conditions consist of a *path* and a *value*, separated by an equal sign (`=`). Values always have to be embraced by double-quotes. Multiple conditions in the same rule are connected via the `&`-operator, multiple restrictions in the same rule are connected via the `|`-operator.

Typically a rule’s syntax is 

.Single constraint rule
[source,text]
----
<scopeName> {
  <condition_set> -> <restriction_set>;
}  
----

This leads to the following structure:

.Multiple constraint rules
[source,text]
----
<scopeName> {
  <rule_1>;
  ...
  <rule_n>;
}  
----

Both conditions and restrictions are composed of a *path* and a *value*. The latter is always enclosed in double quotes.

.Structure of conditions and restrictions
[source,text]
----
<path>="<value>"
----

A *condition* is a way of defining whether a particular situation in {product-name} is based on annotation layers and features in it. Conditions can be defined on features with string, integer or boolean values, but in any case, the value needs to be put into quotes (e.g. `someBooleanFeature="true"`, `someIntegerFeature="2"`).

A *condition set* consists of one or more conditions. They are connected with logical AND as follows. 

[source,text]
----
<condition> & <condition>
----

A *restriction set* defines a set of restrictions which can be applied if a particular condition set is evaluated to true. As multiple restrictions inside one rule are interpreted as conjunctions, they are separated by the `|`-operator. **Restrictions can only be defined on String-valued features that are associated with a tagset.**

[source,text]
----
<restriction> | <restriction>
----

A *path* is composed of one or more steps, separated by a dot. A *step* consists of a *feature selector* and a *type selector*.
*Type selectors* are only applicable while writing the condition part of a rule. They comprise a *layer operator* `@` followed by the type (Lemma, POS, etc).
*Feature selectors* consist of a feature name, e.g.

[source,text]
----
pos.PosValue
----

*Navigation across layers* is possible via 

[source,text]
----
@<shortLayerName>
----

Hereby all annotations of type `<shortLayerName>` at the same position as the current context are found.

The constraint language supports block comments which start with `/*` and end with `*/`. These
comments may span across multiple lines.

[source,text]
----
/* This is a single line comment */

/*
   This is a multi-
   line comment
*/
----

=== Constraint on a single layer

The simplest constraint rules only consider features on a single layer. In the following example, we constraint the values of the *PosValue* feature based on the value of the *coarseValue*  feature.

[source,text]
----
import de.tudarmstadt.ukp.dkpro.core.api.lexmorph.type.pos.POS as Pos;

Pos {
  coarseValue = "NOUN" -> PosValue = "NN" | PosValue = "NNS" | PosValue = "NNP" | PosValue = "NNPS";
  coarseValue = "VERB" -> PosValue = "VB" | PosValue = "VBD" | PosValue = "VBG" | PosValue = "VBN";
}
----


=== Constraint between two span layers

The following simple example of a constraints file re-orders *POS* tags depending on *Lemma* values.
If the Lemma was annotated as `can`, the POS tags `VERB` and `NOUN` are highlighted. If the *Lemma* value is
`the`, the POS tag `DET*` is suggested first. The trick here is the `@Lemma` which tells the system to look for a *Lemma* annotation at the same position as the current *POS*  annotation and then consider the features of that lemma.

[source,text]
----
import de.tudarmstadt.ukp.dkpro.core.api.segmentation.type.Lemma as Lemma;
import de.tudarmstadt.ukp.dkpro.core.api.lexmorph.type.pos.POS as Pos;

Pos {
  @Lemma.value = "can" ->
    coarseValue = "VERB" |
    coarseValue = "NOUN";

  @Lemma.value = "the" ->
    coarseValue = "DET";
}
----

In the UI, the tags that were matched by the constraints are bold and come first in the list of tags:

image::images/constraints.png[align="center"]


=== Constraining a relation layer based on its endpoints

It is possible to constrain the value of a feature on a relation layer based on features of the relation endpoints. Or said differently: you can restrict which relations are possible between certain entities.
In the following example, we will used the pre-defined *Dependency* layer.

----
import de.tudarmstadt.ukp.dkpro.core.api.syntax.type.dependency.Dependency as DEPENDENCY;

DEPENDENCY {
 Governor.pos.PosValue = "NN" & Dependent.pos.PosValue = "DET" -> DependencyType = "det";
}
----

The `DEPENDENCY { ... }` block says that the rules in that block apply to any annotations of the type `...Dependency` as define by the `import`.
Of course you do not need to use the alias `DEPENDENCY` or the `...Dependency` type, you can use any span or relation layer name and an alias
of your choice.

A relation layer (like `...Dependency`) always has two features called `Governor` and `Dependent` which represent the endpoints of the relation.
`Dependent` should be the *TARGET* of the relation (the side where the arrowhead should be) and `Governor` should be the *SOURCE* (i.e. the side without the arrowhead).

So when we read 

----
<RELATION_LAYER> {
 Governor.<FEATURE> = "<VALUE>" ... -> ...
}
----

that means: looking at the current annotation of `<LAYER>`, match if the *TARGET* of the relation has a `<FEATURE>` with the given `<VALUE>`.
Note that in the snippet above, we use not a simple `<FEATURE>` but actually a `<FEATURE_PATH>` `...pos.PosValue` which means that we first go to the annotation referred to by the `pos` feature on the *TARGET* and then continue to the `PosValue` feature.
This is a special situation for the built-in Dependency layer which you do not need when using custom layers.

So the general structure for you to start from could be:

----
<RELATION_LAYER> {
 Governor.<SOURCE_FEATURE> = "<SOURCE_FEATURE_VALUE>" & Dependent.<TARGET_FEATURE> = "<TARGET_FEATURE_VALUE>" -> <RELATION_FEATURE> = "<RELATION_VALUE>"
}
----

[[sect_constraints_conditional_features]]
=== Conditional features

Constraints can be used to set up conditional features, that is features that only become available
in the UI if another feature has a specific value. Let's say that for example you want to annotate
events and only *causing* events should additionally offer a *polarity* feature, while for *caused*
events, there should be no way to select a polarity.

Sticking with the example of annotating events, conditional features can be set up as following:

* Go to the *Layer* tab of the project settings
* Create a new tagset called *Event category* and add the tags *causing* and *caused*
* Create a new tagset called *Event polarity* and add the tags *positive* and *negative*
* Create a new span layer called *Event*
* Add a string feature called *category* and assign the tagset *Event category* 
* Save the changes to the *category* feature
* Add a string feature called *polarity* and assign the tagset *Event polarity* 
* Enabled the checkbox *Hide Un-constraint feature* on the *polarity* feature
* Save the changes to the *polarity* feature
* Create a new text file called `constraints.txt` with the following contents
.
[source,text]
----
import webanno.custom.Event as Event;

Event {
  category="causing" -> polarity="positive" | polarity="negative";
}
----
* Import `constraints.txt` in the tab *Constraints* in the project settings.

When you now annotate an *Event* in this project, then the *polarity* feature is only visible and
editable if the *category* of the annotation is set to *causing*.

NOTE: It is important that both of the features have tagsets assigned - otherwise the conditional
      effect will not take place.

=== Constraints for slot features

Constraints can be applied to the roles of slot features. This is useful, e.g. when annotating predicate/argument structures where specific predicates can only have certain arguments. 

Consider having a span layer `SemPred` resembling a semantic predicate and bearing a slot feature `arguments` and a string feature `senseId`.
We want to restrict the possible argument roles based on the lemma associated with the predicate. 
The first rule in the following example restricts the `senseId` depending on the value of a `Lemma` annotation at the same position as the `SemPred` annotation.
The second rule then restricts the choice of roles for the arguments based on the `senseId`.
Note that to apply a restriction to the role of a slot feature, it is necessary to append `.role` to the feature name (that is because `role` is technically a nested feature). 
Thus, while we can write e.g. `senseId = "Request"` for a simple string feature, it is necessary to write `arguments.role = "Addressee"`.

Note that some role labels are marked with the flag `(!)`.
This is a special flag for slot features and indicates that slots with these role labels should be automatically displayed in the UI ready to be filled.
This should be used for mandatory or common slots and saves time as the annotator does not have to manually create the slots before filling them.

[source,text]
----
SemPred {
  /* Rule 1 */
  @Lemma.value = "ask" -> senseId = "Questioning" | senseId = "Request" | senseId = "XXX";
  /* .. other lemmata */
  /* Rule 2 */
  senseId = "Questioning" -> 
    /* core roles */
    arguments.role = "Addressee" (!) | arguments.role = "Message" (!) | arguments.role = "Speaker" (!) |
    /* non-core roles */
    arguments.role = "Time" | arguments.role = "Iterations";
  /* .. other senses */
}
----

== Constraints language grammar

.Constraints language grammar
[source,text]
----
// Basic structure ---------------------------------------
<file>            ::= <import>* | <scope>*
<scope>           ::= <shortLayerName> "{" <ruleset> "}"
<ruleset>         ::= <rule>*
<import>          ::= "import" <qualifiedLayerName> 
                      "as" <shortLayerName> 
<rule>            ::= <conds> "->" <restrictions> ";"

// Conditions --------------------------------------------
<conds>           ::= <cond> | (<cond> "&" <conds>)
<cond>            ::= <path> "=" <value>
<path>            ::= <featureName> | (<step> "." <path>)
<step>            ::= <featureName> | <layerSelector>
<layerSelector>   ::= <layerOperator>? <shortLayerName>
<layerOperator>   ::= "@" // select annotation in layer X

// Restrictions ------------------------------------------
<restrictions>    ::= <restriction> | 
                      <restriction> "|" <restrictions>
<restriction>     ::= <restrictionPath> "=" <value> 
                      ( "(" <flags> ")" )
<restrictionPath> ::= <featureName> | 
                      <restrictionPath> "." <featureName>
<flags>           ::= "!" // core role
----
